#include "stddefs"
#include "stdmath"
#include "stdmem"

function sleep(int time)
{
    while (time)
        time = decrement(time);
}

const int s_enable = 0x80;
const int s_rs = 0x40;

var int screensignal;
var int screenbuffer[80];

// non-zero if a < b:
// if the top bit is different and only true for b, a must be < b.
function int lessthan(int a, int b)
{
    lessthan = 0;
    var int difference = xor(a, b);
    while (difference)
    {
        if (and(difference, 0x80))
        {
            lessthan = and(b, 0x80);
            return;
        }
        difference = shiftleft(difference);
        a = shiftleft(a);
        b = shiftleft(b);
    }
}

function pointer addPointer(pointer p, int offset)
{
    var int bottom;
    bottom = add(second(p), offset);
    if (lessthan(bottom, second(p)))
        addPointer = pair(increment(first(p)), bottom);
    else
        addPointer = pair(first(p), bottom);
}

function pointer int2hexstr(int x)
{
    var int buffer[3];
    buffer[0] = 'C';
    buffer[1] = '5';
    buffer[2] = 0;
    int2hexstr = buffer;
}

// write data and pulse the clock:
function write4bits(int data)
{
    screensignal = or(and(screensignal, 0xf0), data);
    output(screensignal, debugout);
    output(or(screensignal, s_enable), debugout);
    nop();
    nop();
    output(screensignal, debugout);
}

function write8bits(int data)
{
    var int top = shiftright(data);
    top = shiftright(top);
    top = shiftright(top);
    top = shiftright(top);
    write4bits(top);
    sleep(0);
    write4bits(and(data, 0x0f));
}

function cursorpos(int x, int y)
{
    screensignal = 0x00;
    var int address;
    if (and(y, 0x02))
    {
        if (and(y, 0x01))
            address = 84;
        else
            address = 20;
    }
    else
    {
        if (and(y, 0x01))
            address = 64;
        else
            address = 0;
    }
    address = add(address, x);
    write8bits(or(address, 0x80));
    screensignal = s_rs;
}

function dumpscreenbuffer()
{
    var int ycount = 4, y = 0;
    var pointer p = screenbuffer;
    while (ycount)
    {
        cursorpos(0, y);
        var int xcount = 20;
        while (xcount)
        {
            write8bits(read(p));
            p = incrementPointer(p);
            xcount = decrement(xcount);
        }
        y = increment(y);
        ycount = decrement(ycount);
    }
}

function emptyscreenbuffer()
{
    memset(screenbuffer, ' ', 80);
}

function scrollscreen()
{
    memcpy(screenbuffer, addPointer(screenbuffer, 20), 60);
    memset(addPointer(screenbuffer, 60), ' ', 20);
}

function printtobuffer(pointer text, int pos)
{
    var pointer dest = addPointer(screenbuffer, pos);
    var int c = read(text);
    while (c)
    {
        write(c, dest);
        text = incrementPointer(text);
        c = read(text);
        dest = incrementPointer(dest);
    }
}

function printline(pointer text)
{
    scrollscreen();
    printtobuffer(text, 60);
}

function wakescreen()
{
    screensignal = 0x00;
    write4bits(0x3);
    sleep(255);
    write4bits(0x3);
    sleep(128);
    write4bits(0x3);
    write4bits(0x2);
    write8bits(0x28);
    write8bits(0x0c);
    write8bits(0x01);
    sleep (128);
    screensignal = s_rs;
}

function main()
{
    sleep(255);
    wakescreen();
    emptyscreenbuffer();
    printtobuffer("What's up world! Characters wrap properly 'cause the screen buffer's in RAM.", 0);
    dumpscreenbuffer();

    while (true)
    {
        var int blink = 0;
        while (true)
        {
            if (read(debugin))
                break;
        }

        printline("Test scroll!");
        dumpscreenbuffer();

        while (read(debugin)) {}
    }
}
